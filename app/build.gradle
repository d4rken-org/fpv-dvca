plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'dagger.hilt.android.plugin'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("GMT+1"))

android {
    def packageName = "eu.darken.fpv.dvca"

    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "${packageName}"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        testInstrumentationRunner "eu.darken.fpv.dvca.HiltTestRunner"

        buildConfigField "String", "GITSHA", "\"${gitSha}\""
        buildConfigField "String", "BUILDTIME", "\"${buildTime}\""
    }

    signingConfigs {
        release {}
    }
    def signingPropFile = new File(System.properties['user.home'], ".appconfig/${packageName}/signing.properties")
    if (signingPropFile.canRead()) {
        Properties signingProps = new Properties()
        signingProps.load(new FileInputStream(signingPropFile))
        signingConfigs {
            release {
                storeFile new File(signingProps['release.storePath'])
                keyAlias signingProps['release.keyAlias']
                storePassword signingProps['release.storePassword']
                keyPassword signingProps['release.keyPassword']
            }
        }
    }

    buildTypes {
        def proguardRulesRelease = fileTree(dir: "../proguard", include: ["*.pro"]).asList().toArray()
        debug {
            ext.enableBugsnag = false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles proguardRulesRelease
            proguardFiles 'proguard-rules-debug.pro'
        }
        release {
            signingConfig signingConfigs.release
            lintOptions {
                abortOnError true
                fatal 'StopShip'
                disable 'Instantiatable'
            }
            ext.enableBugsnag = true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles proguardRulesRelease
        }
        applicationVariants.all { variant ->
            if (variant.buildType.name == "debug") {
                variant.mergedFlavor.resourceConfigurations.clear()
                variant.mergedFlavor.resourceConfigurations.add("en")
                variant.mergedFlavor.resourceConfigurations.add("de")
            } else if (variant.buildType.name != "debug") {
                variant.outputs.each { output ->
                    output.outputFileName = "${packageName}-v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ")-" + variant.buildType.name.toUpperCase() + "-" + gitSha + ".apk"
                }
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"

            freeCompilerArgs += [
                    "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                    "-Xuse-experimental=kotlinx.coroutines.FlowPreview",
                    "-Xuse-experimental=kotlin.time.ExperimentalTime",
                    "-Xopt-in=kotlin.RequiresOptIn",
                    "-Xjvm-default=enable"
            ]
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared/java"
        }
        androidTest {
            java.srcDirs += "$projectDir/src/testShared/java"
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")

    implementation("com.squareup.okio:okio:3.0.0-alpha.4")
    implementation 'com.google.android.exoplayer:exoplayer:2.14.0'

    debugImplementation 'com.arthenica:ffmpeg-kit-full:4.4.LTS'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin.core}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlin.coroutines}"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin.core}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlin.coroutines}"
    androidTestImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlin.coroutines}") {
        // conflicts with mockito due to direct inclusion of byte buddy
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }


    // Debugging
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation('com.bugsnag:bugsnag-android:5.9.2')
    implementation 'com.getkeepsafe.relinker:relinker:1.4.3'


    // DI
    implementation "com.google.dagger:dagger:${versions.dagger.core}"
    implementation "com.google.dagger:dagger-android:${versions.dagger.core}"

    kapt "com.google.dagger:dagger-compiler:${versions.dagger.core}"
    kapt "com.google.dagger:dagger-android-processor:${versions.dagger.core}"

    implementation "com.google.dagger:hilt-android:${versions.dagger.core}"
    kapt "com.google.dagger:hilt-android-compiler:${versions.dagger.core}"

    testImplementation "com.google.dagger:hilt-android-testing:${versions.dagger.core}"
    kaptTest "com.google.dagger:hilt-android-compiler:${versions.dagger.core}"

    androidTestImplementation "com.google.dagger:hilt-android-testing:${versions.dagger.core}"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:${versions.dagger.core}"


    // Support libs
    implementation 'androidx.core:core-ktx:1.6.0-alpha03'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.annotation:annotation:1.2.0'

    implementation 'androidx.activity:activity-ktx:1.3.0-alpha07'
    implementation 'androidx.fragment:fragment-ktx:1.3.3'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams-ktx:2.4.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.4.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'

    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"


    // UI
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.3.0'


    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.junit.vintage:junit-vintage-engine:5.7.0"
    testImplementation "androidx.test:core-ktx:1.3.0"

    testImplementation "io.mockk:mockk:1.11.0"
    androidTestImplementation "io.mockk:mockk-android:1.11.0"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"

    androidTestImplementation "androidx.navigation:navigation-testing:2.3.5"

    testImplementation "io.kotest:kotest-runner-junit5:4.5.0"
    testImplementation "io.kotest:kotest-assertions-core-jvm:4.5.0"
    testImplementation "io.kotest:kotest-property-jvm:4.5.0"
    androidTestImplementation "io.kotest:kotest-assertions-core-jvm:4.5.0"
    androidTestImplementation "io.kotest:kotest-property-jvm:4.5.0"

    testImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'android.arch.core:core-testing:1.1.1'
    debugImplementation 'androidx.test:core-ktx:1.3.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.3.0'
}